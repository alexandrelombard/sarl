/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2019 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.core

import java.util.Arrays
import java.util.Collection
import java.util.Collections
import java.util.UUID
import io.sarl.lang.^annotation.EarlyExit
import io.sarl.lang.core.SpaceID
import io.sarl.lang.core.Address
import io.sarl.util.Collections3

/**
 * Initialization event.
 * The source of this event is the agent itself.
 *
 * <h4>For an Agent</h4>
 * This event is received by the agent's native behavior when the agent is spawned.
 *
 * <h4>For a Behavior</h4>
 * Each Behavior instance within the agent receives this event when it is registered for the first time.
 *
 * <h4>For a Skill</h4>
 * No skill receives this event.
 *
 * <h4>For Agent Members</h4>
 * The member agents do not receive this event from the parent agent because they are not yet created when the agent is spawned.
 */
final event Initialize {

	/** The initialization parameters.
	 * Usually, they are the arguments passed to the agent spawning function.
	 */
	val parameters : Object[]

	/** Identifier of the agent, which has spawned the under-initialization agent.
	 * If the under-initialization agent was spawned by the SARL run-time environment
	 * (and not by another agent), the identifier is <code>null</code>.
	 *
	 * @since 0.5
	 */
	val spawner : UUID

	/** Constructor.
	 *
	 * @param spawner the identifier of the spawner of the under-initialization agent.
	 * @param params the initialization parameters to give to the under-initialization agent.
	 * @since 0.5
	 */
	new(spawner : UUID, params : Object*) {
		this(null, spawner, params)
	}

	/** Constructor.
	 *
	 * @param source address of the agent that is emitting this event.
	 * @param spawner the identifier of the spawner of the under-initialization agent.
	 * @param params the initialization parameters to give to the under-initialization agent.
	 * @since 0.5
	 */
	new(source : Address, spawner : UUID, params : Object*) {
		super(source)
		this.spawner = spawner
		this.parameters = params
	}

}

/**
 * Notifies the destruction of an element.
 * The source of this event is the agent itself.
 * This event is marked as an early exit event. It means that any action that is firing this
 * event is supposed to never return, e.g. the <code>killMe</code> action.
 *
 * <h4>For an Agent</h4>
 * This event is received by the agent's native behavior when the agent is destroyed, usually after
 * a call to {@code killMe}.
 * All the agent behaviors of the agent are unregistered prior to the firing of this events.
 * All the agent tasks are canceled prior to the firing of this events.
 * Agent skills are uninstalled after the {@code Destroy} event handler is ran.
 *
 * <h4>For a Behavior</h4>
 * Each Behavior instance within the agent receives this event when it is unregistered for the last time.
 *
 * <h4>For a Skill</h4>
 * No skill receives this event, except if you have registered the skill as an event listener on the
 * agent internal event bus. In the last case, the skill will receive the {@code Destroy} event because
 * is will be uninstalled after its firing.
 *
 * <h4>For Agent Members</h4>
 * The member agents do not receive this event from the parent agent because the parent agent cannot be
 * destroyed if it has agent members.
 */
@EarlyExit
final event Destroy

/**
 * Informs the creation of a new agent in a spawning context.
 * The event is fired within the default space of the spawning context, i.e. the context in which the agent was created.
 * The spawned agent does not receive this event.
 * The source of the event is the spawning agent.
 * The context id can be retrieved via {@code ev.source.spaceID.contextID}.
 */
final event AgentSpawned {

	/** Identifier of the spawned agent.
	 * It is the first identifier in {@link #agentIdentifiers}.
	 *
	 * @deprecated since 0.5, See {@link #agentIdentifiers}.
	 */
	@Deprecated
	val agentID : UUID

	/** Identifiers of the spawned agents.
	 * @since 0.5
	 */
	val agentIdentifiers : Collection<UUID>

	/** Fully qualifiedname of the type of the agent.
	 */
	val agentType : String

	/** Constuct the event.
	 *
	 * @param source the address of the emitter.
	 * @param agentID : the identifier of the spawned agent.
	 * @param agentType fully qualified name of the type of the agent.
	 * @deprecated
	 */
	@Deprecated
	new (source : Address, agentID : UUID, agentType : String) {
		setSource(source)
		this.agentIdentifiers = Collections::singleton(agentID)
		this.agentID = agentID
		this.agentType = agentType
	}

	/** Constuct the event.
	 *
	 * @param source the address of the emitter.
	 * @param agentType fully qualified name of the type of the agent.
	 * @param agentID : the identifiers of the spawned agents.
	 */
	new (source : Address, agentType : String,  agentID : UUID*) {
		setSource(source)
		this.agentIdentifiers = Collections::unmodifiableCollection(Arrays::asList(agentID))
		this.agentID = agentID.get(0)
		this.agentType = agentType
	}

	/** Constuct the event.
	 *
	 * @param source the address of the emitter.
	 * @param agentType fully qualified name of the type of the agent.
	 * @param agentID : the identifiers of the spawned agents.
	 */
	new (source : Address, agentType : String,  agentID : Collection<UUID>) {
		setSource(source)
		this.agentIdentifiers = Collections::unmodifiableCollection(Collections3::serializableCollection(agentID))
		this.agentID = agentID.iterator.next
		this.agentType = agentType
	}

}

/**
 * Informs an agent has been killed.
 * The event is fires within all the external contexts (including the default context) of the killed agent.
 * The killed agent does not receive this event.
 * The source of the event is the killed agent.
 * The context id can be retrieved via {@code ev.source.spaceID.contextID}.
 */
final event AgentKilled {

	/** The identifier of the killed agent.
	 * @deprecated since 0.7, See the event source.
	 */
	@Deprecated
	val agentID : UUID

	/** The fully qualified name of the type of the killed agent.
	 */
	val agentType : String

	/** Construct the event.
	 *
	 * @param source the address of the emitter.
	 * @param agentID the identifier of the killed agent.
	 * @param agentType fully qualified name of the type of the agent.
	 */
	new (source : Address, agentID : UUID, agentType : String) {
		setSource(source)
		this.agentID = agentID
		this.agentType = agentType
	}

}

/**
 * Informs the creation of a new space in a given context.
 * The event is fired within the default space of the context in which the space was created.
 * The source of the event is the parent agent.
 * The context id can be retrieved via {@code ev.source.spaceID.contextID}.
 */
final event SpaceCreated {

	/** Identifier of the created space.
	 */
	val spaceID : SpaceID

	/** Construct the event.
	 *
	 * @param source the address of the emitter.
	 * @param spaceID the identifier of the space.
	 */
	new (source : Address, spaceID : SpaceID) {
		setSource(source)
		this.spaceID = spaceID
	}

}

/** Informs the destruction of a space in a given context.
 * The event is fired within the default space of the context from  which the space was removed.
 * The context id can be retrieved via {@code ev.source.spaceID.contextID}.
 * The source of the event is the parent agent.
 */
final event SpaceDestroyed {

	/** The identifier of the destroyed space.
	 */
	val spaceID : SpaceID

	/** Construct the event.
	 *
	 * @param source the address of the emitter.
	 * @param spaceID the identifier of the space.
	 */
	new (source : Address, spaceID : SpaceID) {
		setSource(source)
		this.spaceID = spaceID
	}

}

/** Notifies other members the member with agentID has effectively 
 * integrated the holon context.
 * The event is fired within the default space of the context in which the new agent entered.
 * The joining agent does not receive this event.
 * The source of the event is the parent agent.
 * The context id can be retrieved via {@code ev.source.spaceID.contextID}.
 */
final event MemberJoined {

	/** The identifier of the context that the member joined.
	 * @deprecated See the event source
	 */
	@Deprecated
	val parentContextID : UUID

	/** The identifier of the agent that has joined the context.
	 */
	val agentID : UUID

	/** The fully qualified name of the type of the agent.
	 */
	val agentType : String

	/** Construct the event.
	 *
	 * @param source the address of the emitter.
	 * @param contextID the identifier of the context.
	 * @param agentID the identifier of the agent.
	 * @param agentType fully qualified name of the type of the agent.
	 */
	new (source : Address, contextID : UUID, agentID : UUID, agentType : String) {
		setSource(source)
		this.parentContextID = contextID
		this.agentID = agentID
		this.agentType = agentType
	}

}

/** Notifies other members the member with agentID was 
 * excluded (forced to leave) the holon context parentContextID.
 * The event is fired within the default space of the context from which the agent was excluded.
 * The excluded agent does not receive this event.
 * The source of the event is the parent agent.
 * The context id can be retrieved via {@code ev.source.spaceID.contextID}.
 */
final event MemberExcluded {

	/** The identifier of the context from which the member was excluded.
	 * @deprecated See source
	 */
	@Deprecated
	val parentContextID : UUID

	/** The identifier of the agent that was excluded from the context.
	 */
	val agentID : UUID

	/** The fully qualified name of the type of the agent.
	 */
	val agentType : String

	/** Construct the event.
	 *
	 * @param source the address of the emitter.
	 * @param contextID the identifier of the context.
	 * @param agentID the identifier of the agent.
	 * @param agentType fully qualified name of the type of the agent.
	 */
	new (source : Address, contextID : UUID, agentID : UUID, agentType : String) {
		setSource(source)
		this.parentContextID = contextID
		this.agentID = agentID
		this.agentType = agentType
	}

}

/** Notifies other members the member with agentID has left the holon context.
 * The event is fired within the default space of the context from which the agent has left.
 * The left agent does not receive this event.
 * The source of the event is the parent agent.
 * The context id can be retrieved via {@code ev.source.spaceID.contextID}.
 */
final event MemberLeft {

	/** The identifier of the agent that has left the holon context.
	 */
	val agentID : UUID

	/** The fully qualified name of the type of the agent.
	 */
	val agentType : String

	/** Construct the event.
	 *
	 * @param source the address of the emitter.
	 * @param agentID the identifier of the agent.
	 * @param agentType fully qualified name of the type of the agent.
	 */
	new (source : Address, agentID : UUID, agentType : String) {
		setSource(source)
		this.agentID = agentID
		this.agentType = agentType
	}

}

/** Notifies behaviors the agent has joined a new super-holon.
 * The event is fired within the internal bus of the agent that have joined a super-holon.
 * The source of the event is the agent itself.
 */
final event ContextJoined {

	/** The identifier of the context in the super-holon.
	 */
	val holonContextID : UUID

	/** The identifier of the default space in the super-holon context.
	 */
	val defaultSpaceID : UUID

	/** Construct the event.
	 *
	 * @param contextID the identifier of the context.
	 * @param defaultSpaceID the identifier of the default space.
	 */
	new (contextID : UUID, defaultSpaceID : UUID) {
		this.holonContextID = contextID
		this.defaultSpaceID = defaultSpaceID
	}

}

/**
 * Notifies behaviors the agent has left a super-holon.
 * The event is fired within the internal bus of the agent that have left a super-holon.
 * The source of the event is the agent itself.
 */
final event ContextLeft {

	/** The identifier of the context in the super-holon.
	 */
	val holonContextID : UUID

	/** Construct the event.
	 *
	 * @param contextID the identifier of the context.
	 */
	new (contextID : UUID) {
		this.holonContextID = contextID
	}

}

